composer parseInt
  <INT>
end parseInt

def input: idx´0:[$IN::lines -> parseInt];

templates mix&{file:}
  @: {index: 0:[$file::first..$file::last],
    reverse: $file::first:[0..~$file::length]};
  1..$ -> $file::first -> #
  $file($@.index) -> \[i](<=0> $i!\)... -> $::raw
  -> $file($@.index(($+1000) mod $file::length)) + $file($@.index(($+2000) mod $file::length)) + $file($@.index(($+3000) mod $file::length))!
  
  when <$file::last~..> do !VOID
  otherwise
    def i: $@.reverse($);
    def offset: $file($) mod ($file::length - 1) -> \(
      <..$file::length ~/ 2> $!
      otherwise $ - ($file::length - 1)!
    \);
    [$i..~$i+$offset, $i~..$i+$offset:-1]... -> \(
        def this: $ mod $file::length;
        def next: ($this+1) mod $file::length;
        @mix.index([$this, $next]): $@mix.index([$next, $this])...;
        @mix.reverse($@mix.index([$this, $next])): [$this, $next]...;
    \) -> !VOID
    idx´($::raw + 1) -> #
end mix

source solutionPart1
  1 -> mix&{file: $input}!
end solutionPart1

source solutionPart2
  10 -> mix&{file: $input -> \[i]($ * 811589153!\)}!
end solutionPart2

'$:'part' -> SYS::property;' -> \(
  when <='part1'|=''> do $solutionPart1 !
  when <='part2'> do $solutionPart2 !
  otherwise 'Unknown part $;' !
\) -> '$;
' -> !OUT::write
